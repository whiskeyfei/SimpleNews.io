apply plugin: 'com.android.application'
//该 plugin 为一个 com.android.application 程序，也就是应用程序，如果你的 plugin 是一个库，那么自然也就是apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion //编译的 SDK API 版本
    buildToolsVersion rootProject.ext.android.buildToolsVersion //构建工具版本

    //默认配置&核心属性，该属性会重写在AndroidManifest.xml中的对应属性
    defaultConfig {
        applicationId rootProject.ext.android.applicationId//配置包名的
        minSdkVersion rootProject.ext.android.minSdkVersion //支持最小的 SDK 版本
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode//版本号
        versionName rootProject.ext.android.versionName//版本名称

//        manifestPlaceholders = [PRODUCT_CHANNEL_VALUE: "github"]// 默认
    }

    signingConfigs {
        releaseConfig {
            keyAlias rootProject.ext.releaseConfig.keyAlias
            keyPassword rootProject.ext.releaseConfig.keyPassword
            storeFile file(rootProject.ext.releaseConfig.storeFile)
            storePassword rootProject.ext.releaseConfig.storePassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }

        release {
            zipAlignEnabled true
            minifyEnabled true //是否启动混淆
            shrinkResources true //是否移除无用资源文件，shrinkResources依赖于minifyEnabled
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig

            android.applicationVariants.all { variant ->
                    variant.outputs.each { output ->
                        def outputFile = output.outputFile
                        if (outputFile != null && outputFile.name.endsWith('.apk')) {
                            def fileName = "SimpleNews_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                            output.outputFile = new File(outputFile.parent, fileName)
                        }
                    }
            }

            //多渠道
            productFlavors.all { flavor ->
                manifestPlaceholders.put("UMENG_CHANNEL_VALUE", name)
            }
        }
    }

    productFlavors {
        product1 {
            manifestPlaceholders.put("PRODUCT_CHANNEL_VALUE", 'product1')
        }
        product2 {
            manifestPlaceholders.put("PRODUCT_CHANNEL_VALUE", 'product2')
        }
    }

}

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    //将libs文件夹中所有的jar文件视为依赖包。
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    compile rootProject.ext.dependencies.design
    compile rootProject.ext.dependencies.cardview
    compile rootProject.ext.dependencies.circleimageview
    compile rootProject.ext.dependencies.sufficientlysecure

    compile project(':lib.utils')
    compile project(':lib.style')

    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'com.afollestad.material-dialogs:commons:0.9.1.0'

    compile 'me.drakeet.multitype:multitype:3.1.0-beta1'
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
